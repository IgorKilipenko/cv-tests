## Задание

### Разработка движений документа *Поступление товаров и услуг* по регистрам:

- **Регистр накопления `ТоварыНаСкладах`**: Заполнение реквизитов — Склад, Номенклатура, Срок годности, Количество, Сумма. Источник данных — ТЧ Товары. Срок годности необходимо записывать в регистр, если вид учетной политики установлен на FEFO.
- **Регистр накопления `УчетЗатрат`**: Заполнение реквизитов — Статья затрат, Сумма. Источник данных — ТЧ услуги. Движения необходимо сворачивать по статье затрат.
- **Регистр бухгалтерии `Хозрасчетный`**: Документ должен формировать движения:

| Проводка            | Содержание операции                                                          |
|---------------------|------------------------------------------------------------------------------|
| Дт 10 (41) Кт 60    | Отражено поступление товарно-материальных ценностей от поставщика            |
| Дт 44 (91) Кт 60    | Отражено поступление услуг от поставщика                                     |

> Счёт по дебету проводки необходимо получать из реквизита *Счёт бухгалтерского учёта номенклатуры*. 
> Субконто счетов должно заполняться соответствующими данными из документа.

## Решение

### Документы.ПоступлениеТоваровИУслуг.МодульОбъекта

```bsl
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(_, __)
    выполнитьВсеДвиженияДокумента();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

#Область СлужебныеПроцедурыИФункции

#Область Движения
Процедура выполнитьВсеДвиженияДокумента()
    ЭтотОбъект.Движения.ТоварыНаСкладах.Записывать = Истина;
    ЭтотОбъект.Движения.УчетЗатрат.Записывать = Истина;
    ЭтотОбъект.Движения.Хозрасчетный.Записывать = Истина;

    выполнитьВсеДвиженияТоваровДокумента();
    выполнитьВсеДвиженияУслугДокумента();
КонецПроцедуры

Процедура выполнитьВсеДвиженияТоваровДокумента()
    отражатьСрокиГодности = получитьУчетнуюПолитику() = Перечисления.СпособыСписанияЗапасов.FEFO;

    таблицаТоварыДокумента = Документы.ПоступлениеТоваровИУслуг.ПолучитьТоварыДокумента(
            ЭтотОбъект.Ссылка, отражатьСрокиГодности, Истина);
    Если таблицаТоварыДокумента = Неопределено Тогда
        ДиагностикаКлиентСервер.Утверждение(ЭтотОбъект.Товары.Количество() = 0,
                "Ожидается что ТЧ Товары должна быть пустой.");
        Возврат;
    КонецЕсли;

    Для Каждого строкаТоваров Из таблицаТоварыДокумента Цикл
        выполнитьДвижениеТоварыНаСкладахПриход(строкаТоваров, отражатьСрокиГодности);
        ЭтотОбъект.Движения.Хозрасчетный.ДобавитьЗаписьПоступлениеТМЦ(ЭтотОбъект.Дата, строкаТоваров, ЭтотОбъект.Контрагент);
    КонецЦикла;
КонецПроцедуры

Процедура выполнитьВсеДвиженияУслугДокумента()
    таблицаУслуг = Документы.ПоступлениеТоваровИУслуг.ПолучитьУслугиДокумента(
            ЭтотОбъект.Ссылка, Истина);

    Для Каждого строкаУслуг Из таблицаУслуг Цикл
            ЭтотОбъект.Движения.Хозрасчетный.ДобавитьЗаписьПоступлениеУслуг(ЭтотОбъект.Дата, строкаУслуг, ЭтотОбъект.Контрагент);
    КонецЦикла;

    таблицаУслуг.Свернуть("СтатьяЗатрат", "Сумма");
    таблицаСтатьиЗатрат = таблицаУслуг;
    Для Каждого строкаЗатрат Из таблицаСтатьиЗатрат Цикл
        выполнитьДвижениеУчетЗатратОборот(строкаЗатрат);
    КонецЦикла;
КонецПроцедуры

// Параметры:
//  строкаТоваров - СтрокаТаблицыЗначений, Структура
//      * Номенклатура - СправочникСсылка.Номенклатура
//      * Количество - Число
//      * Сумма - Число
//      * СрокГодности - Дата, Неопределено - [Опционально] Если `отражатьСрокиГодности` = Ложь значение игнорируется
//  отражатьСрокиГодности - Булево - Если Ложь, `СрокГодности` игнорируется
Процедура выполнитьДвижениеТоварыНаСкладахПриход(Знач строкаТоваров, Знач отражатьСрокиГодности = Ложь)
    движение = ЭтотОбъект.Движения.ТоварыНаСкладах.Добавить();
    движение.ВидДвижения = ВидДвиженияНакопления.Приход;
    движение.Период = ЭтотОбъект.Дата;
    движение.Номенклатура = строкаТоваров.Номенклатура;
    движение.Склад = ЭтотОбъект.Склад;
    движение.Количество = строкаТоваров.Количество;
    движение.Сумма = строкаТоваров.Сумма;
    Если отражатьСрокиГодности Тогда
        движение.СрокГодности = строкаТоваров.СрокГодности;
    КонецЕсли;
КонецПроцедуры

// Параметры:
//  строкаЗатрат - СтрокаТаблицыЗначений, Структура
//      * СтатьяЗатрат - СправочникСсылка.СтатьиЗатрат
//      * Сумма - Число
Процедура выполнитьДвижениеУчетЗатратОборот(Знач строкаЗатрат)
    движение = ЭтотОбъект.Движения.УчетЗатрат.Добавить();
    движение.Период = ЭтотОбъект.Дата;
    движение.СтатьяЗатрат = строкаЗатрат.СтатьяЗатрат;
    движение.Сумма = строкаЗатрат.Сумма;
КонецПроцедуры
#КонецОбласти // Движения

// Возвращаемое значение:
//  - ПеречислениеСсылка.СпособыСписанияЗапасов
Функция получитьУчетнуюПолитику()
    Возврат РегистрыСведений.УчетнаяПолитика.ПолучитьТекущийСпособСписанияЗапасов(ЭтотОбъект.Дата);
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
```

### Документы.ПоступлениеТоваровИУслуг.МодульМенеджера

```bsl
#Область ПрограммныйИнтерфейс

// Возвращает выборку товаров документа сгруппированных по Номенклатуре
//  и сроку годности если аргумент: отражатьСрокиГодности = Истина
// Параметры:
//  документСсылка - ДокументСсылка.ПоступлениеТоваровИУслуг
//  отражатьСрокиГодности - Булево - Если Истина тогда будет выполнена группировка по срокам годности
//  выгрузить - Булево - Если Истина результат запроса будет выгружен в `ТаблицуЗначений`
// Возвращаемое значение:
//  - ВыборкаИзРезультатаЗапроса
//  - ТаблицаЗначений
//      * Номенклатура - СправочникСсылка.Номенклатура
//      * Количество - Число
//      * Сумма - Число - Общая стоимость позиции
//      * СрокГодности - Дата, Неопределено - [Опциональное] в случае если отражатьСрокиГодности = Ложь - поле отсутствует в выборке
//  - Неопределено
Функция ПолучитьТоварыДокумента(Знач документСсылка, Знач отражатьСрокиГодности, Знач выгрузить = Истина) Экспорт
    запросТовары = Новый Запрос;
    запросТовары.УстановитьПараметр("Ссылка", документСсылка);
    запросТовары.Текст =
        "ВЫБРАТЬ
        |	ПоступлениеТоваровТовары.Номенклатура КАК Номенклатура,
        |	СУММА(ПоступлениеТоваровТовары.Количество) КАК Количество,
        |   ПоступлениеТоваровТовары.СрокГодности КАК СрокГодности,
        |   ПоступлениеТоваровТовары.Номенклатура.СчетБухгалтерскогоУчета КАК СчетБухгалтерскогоУчета,
        |	СУММА(ПоступлениеТоваровТовары.Сумма) КАК Сумма
        |ИЗ
        |	Документ.ПоступлениеТоваровИУслуг.Товары КАК ПоступлениеТоваровТовары
        |ГДЕ
        |	ПоступлениеТоваровТовары.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |	&ПоляГруппировки
        |";

    поляГруппировки =
        "   ПоступлениеТоваровТовары.Номенклатура,
        |	ПоступлениеТоваровТовары.СрокГодности,
        |   ПоступлениеТоваровТовары.Номенклатура.СчетБухгалтерскогоУчета
        |";

    Если НЕ отражатьСрокиГодности Тогда // Удаляем поле СрокГодности из запроса
        запросТовары.Текст = СтрЗаменить(запросТовары.Текст,
                "ПоступлениеТоваровТовары.СрокГодности КАК СрокГодности,", "");
        поляГруппировки = СтрЗаменить(поляГруппировки,
                "ПоступлениеТоваровТовары.СрокГодности,", "");
    КонецЕсли;

    запросТовары.Текст = СтрЗаменить(запросТовары.Текст, "&ПоляГруппировки", поляГруппировки);

    результатЗапроса = запросТовары.Выполнить();
    Если результатЗапроса.Пустой() Тогда
        Возврат Неопределено;
    КонецЕсли;

    Если выгрузить = Истина Тогда
        Возврат результатЗапроса.Выгрузить();
    Иначе
        Возврат результатЗапроса.Выбрать();
    КонецЕсли;
КонецФункции

// Выполняет запрос из ТЧ `Услуги` документа с группировкой по полям: `СтатьяЗатрат`, `Номенклатура`, `СчетБухгалтерскогоУчета`
// Параметры:
//  документСсылка - ДокументСсылка.ПоступлениеТоваровИУслуг
//  выгрузить - Булево - Если Истина будет выполнена Выгрузка данных в таблицу значений
// Возвращаемое значение:
//  - ТаблицаЗначений - Если `выгрузить` = Истина
//  - ВыборкаИзРезультатаЗапроса - Если `выгрузить` = Ложь
Функция ПолучитьУслугиДокумента(Знач документСсылка, Знач выгрузить = Истина) Экспорт
    запрос = Новый Запрос;
    запрос.УстановитьПараметр("Ссылка", документСсылка);
    запрос.Текст =
        "ВЫБРАТЬ
        |	ПоступлениеУслугУслуги.Номенклатура КАК Номенклатура,
        |	ПоступлениеУслугУслуги.СтатьяЗатрат КАК СтатьяЗатрат,
        |   ПоступлениеУслугУслуги.Номенклатура.СчетБухгалтерскогоУчета КАК СчетБухгалтерскогоУчета,
        |	СУММА(ПоступлениеУслугУслуги.Сумма) КАК Сумма
        |ИЗ
        |	Документ.ПоступлениеТоваровИУслуг.Услуги КАК ПоступлениеУслугУслуги
        |ГДЕ
        |	ПоступлениеУслугУслуги.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |   ПоступлениеУслугУслуги.СтатьяЗатрат,
        |   ПоступлениеУслугУслуги.Номенклатура,
        |   ПоступлениеУслугУслуги.Номенклатура.СчетБухгалтерскогоУчета
        |";

    результатЗапроса = запрос.Выполнить();

    Если выгрузить = Истина Тогда
        Возврат результатЗапроса.Выгрузить();
    Иначе
        Возврат результатЗапроса.Выбрать();
    КонецЕсли;
КонецФункции

// Возвращает выборку услуг документа с группировкой по Номенклатуре и Итогами по СтатьямЗатрат
// Параметры:
//  документСсылка - ДокументСсылка.ПоступлениеТоваровИУслуг
//  выгрузить - Булево - по умолчанию = Истина
// Возвращаемое значение:
//  - ВыборкаИзРезультатаЗапроса, ТаблицаЗначений
//      * Номенклатура - СправочникСсылка.Номенклатура
//      * СтатьяЗатрат - СправочникСсылка.СтатьиЗатрат, Неопределено
//      * Сумма - Число - Общая стоимость позиции
//  - Неопределено
Функция ПолучитьСтатьиЗатратДокумента(Знач документСсылка, выгрузить = Истина) Экспорт
    запросУслуг = Новый Запрос;
    запросУслуг.УстановитьПараметр("Ссылка", документСсылка);

    запросУслуг.Текст =
        "ВЫБРАТЬ
        |	ПоступлениеУслугУслуги.СтатьяЗатрат КАК СтатьяЗатрат,
        |	СУММА(ПоступлениеУслугУслуги.Сумма) КАК Сумма
        |ИЗ
        |	Документ.ПоступлениеТоваровИУслуг.Услуги КАК ПоступлениеУслугУслуги
        |ГДЕ
        |	ПоступлениеУслугУслуги.Ссылка = &Ссылка
        |
        |СГРУППИРОВАТЬ ПО
        |   ПоступлениеУслугУслуги.СтатьяЗатрат
        |";

    результатЗапроса = запросУслуг.Выполнить();

    Если результатЗапроса.Пустой() Тогда
        Возврат Неопределено;
    КонецЕсли;

    Если выгрузить = Истина Тогда
        Возврат результатЗапроса.Выгрузить();
    Иначе
        Возврат результатЗапроса.Выбрать();
    КонецЕсли;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
```

### РегистрыБухгалтерии.Хозрасчетный.МодульНабораЗаписей

```bsl
#Область ПрограммныйИнтерфейс

// Добавляет новую запись в регистр бухгалтерии `Хозрасчетный` для учета поступления услуг.
//  * Счет дебета: Устанавливается из аргумента `строкаУслуг.СчетБухгалтерскогоУчета`
//  * Счет кредита: `ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками`
// Параметры:
//  дата - Дата - Дата записи
//  строкаУслуг - Структура, ФиксированнаяСтруктура
//      * СчетБухгалтерскогоУчета - ПланСчетовСсылка.Хозрасчетный - Счет дебита
//      * СтатьяЗатрат - СправочникСсылка.СтатьиЗатрат - Отражается в субконто дебета
//      * Сумма - Число
//  поставщик - СправочникСсылка.Контрагенты - отражается в субконто кредита
// Возвращаемое значение:
//  - РегистрБухгалтерииЗапись - Заполненная запись
Функция ДобавитьЗаписьПоступлениеУслуг(Знач дата, Знач строкаУслуг, Знач поставщик) Экспорт
    новаяЗапись = ЭтотОбъект.Добавить();
    новаяЗапись.Период = дата;
    новаяЗапись.СчетДт = строкаУслуг.СчетБухгалтерскогоУчета;
    новаяЗапись.СчетКт = ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками;
    новаяЗапись.Сумма = строкаУслуг.Сумма;
    новаяЗапись.Содержание = "Отражено поступление услуг от поставщика";

    // Заполнение субконто дебета
    субконтоЗаполнен = Ложь;
    субконтоЗаполнен = заполнитьСубконтоПоСчету(новаяЗапись.СчетДт, новаяЗапись.СубконтоДт, строкаУслуг.СтатьяЗатрат);
    ДиагностикаКлиентСервер.Утверждение(субконтоЗаполнен ИЛИ новаяЗапись.СчетДт = ПланыСчетов.Хозрасчетный.ПрочиеДоходыИРасходы,
        СтрШаблон("Для счета ""%1"" субконто дебета должен быть заполнен.", Строка(новаяЗапись.СчетДт)));

    // Заполнение субконто кредита
    субконтоЗаполнен = заполнитьСубконтоПоСчету(новаяЗапись.СчетКт, новаяЗапись.СубконтоКт, поставщик);
    ДиагностикаКлиентСервер.Утверждение(субконтоЗаполнен,
        СтрШаблон("Для счета ""%1"" субконто кредита должен быть заполнен.", Строка(новаяЗапись.СчетКт)));

    Возврат новаяЗапись;
КонецФункции

// Добавляет новую запись в регистр бухгалтерии `Хозрасчетный` для учета поступления товаров и материалов
//  * Счет дебета: - Устанавливается из аргумента `строкаТоваров.СчетБухгалтерскогоУчета`
//  * Счет кредита: `ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками`
// Параметры:
//  дата - Дата - Дата записи
//  строкаТоваров - Структура, ФиксированнаяСтруктура, СтрокаТаблицыЗначений
//      * Номенклатура - СправочникСсылка.Номенклатура - отражается в субконто дебета
//      * СчетБухгалтерскогоУчета - ПланСчетовСсылка.Хозрасчетный - Счет дебета
//      * Сумма - Число
//  поставщик - СправочникСсылка.Контрагенты - отражается в субконто кредита
// Возвращаемое значение:
//  - РегистрБухгалтерииЗапись - Заполненная запись
Функция ДобавитьЗаписьПоступлениеТМЦ(Знач дата, Знач строкаТоваров, Знач поставщик) Экспорт
    новаяЗапись = ЭтотОбъект.Добавить();
    новаяЗапись.Период = дата;
    новаяЗапись.СчетДт = строкаТоваров.СчетБухгалтерскогоУчета;
    новаяЗапись.СчетКт = ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками;
    новаяЗапись.Сумма = строкаТоваров.Сумма;
    новаяЗапись.Содержание = "Отражено поступление товарно-материальных ценностей от поставщика";

    // Заполнение субконто дебета
    субконтоЗаполнен = Ложь;
    субконтоЗаполнен = заполнитьСубконтоПоСчету(новаяЗапись.СчетДт, новаяЗапись.СубконтоДт, строкаТоваров.Номенклатура);
    ДиагностикаКлиентСервер.Утверждение(субконтоЗаполнен,
        СтрШаблон("Для счета ""%1"" субконто дебета должен быть заполнен.", Строка(новаяЗапись.СчетДт)));

    // Заполнение субконто кредита
    субконтоЗаполнен = заполнитьСубконтоПоСчету(новаяЗапись.СчетКт, новаяЗапись.СубконтоКт, поставщик);
    ДиагностикаКлиентСервер.Утверждение(субконтоЗаполнен,
        СтрШаблон("Для счета ""%1"" субконто кредита должен быть заполнен.", Строка(новаяЗапись.СчетКт)));

    Возврат новаяЗапись;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

#Область ПомощникиПроводок
// Параметры:
//  счет - ПланСчетовСсылка.Хозрасчетный
// Возвращаемое значение:
//  - ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные
Функция получитьВидСубконтоПоСчету(Знач счет)
    видыСубконто = счет.ВидыСубконто;

    Если видыСубконто.Количество() = 0 Тогда
        Возврат Неопределено;
    КонецЕсли;

    Возврат видыСубконто[0].ВидСубконто;
КонецФункции

// Параметры:
//  счет - ПланСчетовСсылка.Хозрасчетный
//  субконто - РегистрБухгалтерииСубконто
//  значение - СправочникСсылка
// Возвращаемое значение:
//  - Булево
Функция заполнитьСубконтоПоСчету(Знач счет, Знач субконто, Знач значение) Экспорт
    видСубконто = получитьВидСубконтоПоСчету(счет);
    Если видСубконто = Неопределено Тогда
        Возврат Ложь;
    КонецЕсли;

    субконто[видСубконто] = значение;
    Возврат Истина;
КонецФункции
#КонецОбласти // ПомощникиПроводок

#КонецОбласти // СлужебныеПроцедурыИФункции
```

[Полный код реализации документа](https://github.com/IgorKilipenko/onec-auto-repair-shop/tree/main/src/Documents/%D0%9F%D0%BE%D1%81%D1%82%D1%83%D0%BF%D0%BB%D0%B5%D0%BD%D0%B8%D0%B5%D0%A2%D0%BE%D0%B2%D0%B0%D1%80%D0%BE%D0%B2%D0%98%D0%A3%D1%81%D0%BB%D1%83%D0%B3/Ext)